plugins {
  id "com.jfrog.bintray" version "1.7.1"
}

class Conf {
  static group   = 'io.vulpine.lib'
  static version = '0.1.1'
  static name    = 'simple-sql'
  static repo    = 'lib-simple-sql'
  static jvm     = '1.6'
}

group Conf.group
version Conf.version

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'

/*
 * BinTray Configuration
 */
bintray {
  user = System.getenv('BINTRAY_USER')
  key = System.getenv('BINTRAY_KEY')
  publications = ['maven']
  pkg {
    repo = 'maven'
    name = Conf.name
    userOrg = 'vpx'
    licenses = ['Apache-2.0']
    vcsUrl = "https://bitbucket.org/Vulpine/${Conf.name}"
    version {
      name = Conf.version
      released = new Date()
      vcsTag = Conf.version
    }
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.14.1'
  distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}
 
repositories {
}

dependencies {
}

def mApp = { Node node, Map vals ->
  vals.forEach({ k, v -> node.appendNode(k, v) })
  return node
}
def nApp = { node, child ->
  node.append(child)
  return node
}

publishing {
  repositories {
    maven {
      url "https://api.bintray.com/maven/vpx/maven/${Conf.name}/;publish=1"
    }
  }
  publications {
    maven(MavenPublication) {
      from components.java
      pom.withXml {
        { project ->

          project.appendNode('inceptionYear', '2016')
          mApp(
            project.appendNode('organization'),
            [name: 'Vulpine IO', url: 'http://vulpine.io']
          );

          nApp(
            project.appendNode('developers'),
            mApp(new Node(null, 'developer'), [id: 'elliefops', name: 'Elizabeth Harper', email: 'elliefops@gmail.com'])
          );

          mApp(
            project.appendNode('issueManagement'),
            [system: 'YouTrack', url: "http://vulpine.myjetbrains.com/youtrack/issues?q=project%3A+${Conf.repo}"]
          );

          mApp(
            project.appendNode('ciManagement'),
            [system: 'Jenkins', url: "http://ec2-54-87-131-122.compute-1.amazonaws.com/job/${Conf.repo}/"]
          );

          mApp(
            project.appendNode('scm'),
            [
              connection: "scm:git:https://bitbucket.org/Vulpine/${Conf.name}.git",
              developerConnection: "scm:git:https://bitbucket.org/Vulpine/${Conf.name}.git",
              tag: 'master',
              url: "https://bitbucket.org/Vulpine/${Conf.name}"
            ]
          );

          nApp(
            project.appendNode('licenses'),
            mApp(
              new Node(null, 'license'),
              [
                name: 'Apache License, Version 2.0',
                url: 'http://www.apache.org/licenses/LICENSE-2.0.txt',
                distribution: 'repo'
              ]
            )
          );
          return project;
        }.call(asNode())


      }
    }
  }
}

